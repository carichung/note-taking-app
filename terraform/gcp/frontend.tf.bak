resource "google_storage_bucket" "frontend_bucket" {
  name     = "note-taking-frontend"
  location = var.region
  website {
    main_page_suffix = "index.html"
    not_found_page   = "index.html"
  }

  uniform_bucket_level_access = true
}

resource "google_storage_bucket_iam_member" "public_access" {
  bucket = google_storage_bucket.frontend_bucket.name
  role   = "roles/storage.objectViewer"
  member = "allUsers"
}

resource "google_storage_bucket_object" "frontend_files" {
  for_each = fileset("${path.module}/../frontend/note-taking-frontend-react/dist", "**/*")

  name   = each.value
  bucket = google_storage_bucket.frontend_bucket.name
  source = "${path.module}/../frontend/note-taking-frontend-react/dist/${each.value}"
  content_type = lookup(
    {
      html = "text/html"
      css  = "text/css"
      js   = "application/javascript"
      json = "application/json"
      png  = "image/png"
      jpg  = "image/jpeg"
      jpeg = "image/jpeg"
      svg  = "image/svg+xml"
    },
    split(".", each.value)[length(split(".", each.value)) - 1],
    "application/octet-stream"
  )
}
